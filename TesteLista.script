// Script:    TesteLista.script
// Autor:     Arndt von Staa
// Data:      02/abr/2008
// Propósito: Testar superficialmente o módulo lista genérica duplamente encadeada
//

== Inicializar valores
=declararparm   lista0  int 0
=declararparm   lista1  int 1
=declararparm   lista2  int 2

=declararparm   char0    char  'a'
=declararparm   char1    char  'b'
=declararparm   char2    char  'c'
=declararparm   char3    char  'd'

=declararparm   OK             int  0
=declararparm   ListaVazia     int  1
=declararparm   FimLista       int  2
=declararparm   NaoAchou       int  3
=declararparm   FaltouMemoria  int  4

=declararparm   cNULL          char  '\0'

== Criar a lista 0
=criarlista      lista0 

== Verificar ObterValor relativo a lista vazia
=obtervalorelem  lista0   cNULL

== Inserir elemento em lista vazia
=inselemantes    lista0   char0   OK
=obtervalorelem  lista0   char0

== Inserir elemento após primeiro
=inselemapos     lista0   char1   OK
=obtervalorelem  lista0   char1

== Inserir elemento antes do último
=inselemantes    lista0   char2   OK
=obtervalorelem  lista0   char2

== Excluir elemento do meio
=obtervalorelem  lista0   char2
=excluirelem     lista0             OK

== Excluir elemento do início
=obtervalorelem  lista0   char0
=excluirelem     lista0             OK

== Excluir elemento final, único 
=obtervalorelem  lista0   char1
=excluirelem     lista0             OK

== Ver se a lista ficou vazia
=obtervalorelem  lista0   cNULL

== Destruir a lista
=destruirlista   lista0

== Criar uma lista com dois elementos
=criarlista      lista1 
=inselemapos     lista1   char0   OK
=inselemapos     lista1   char1   OK

== Esvaziar a lista e ver se ficou vazia
=esvaziarlista   lista1
=obtervalorelem  lista1   cNULL

== Destruir a lista
=destruirlista   lista1

== Criar uma lista com quatro elementos
=criarlista      lista2 
=inselemapos     lista2   char0   OK
=inselemapos     lista2   char1   OK
=inselemapos     lista2   char2   OK
=inselemapos     lista2   char3   OK

== Verificar a lista de trás para diante
=obtervalorelem  lista2   char3
=avancarelem     lista2   -1     OK
=obtervalorelem  lista2   char2
=avancarelem     lista2   -1     OK
=obtervalorelem  lista2   char1
=avancarelem     lista2   -1     OK
=obtervalorelem  lista2   char0
=avancarelem     lista2   -1     FimLista

== Verificar a lista de frente para trás
=obtervalorelem  lista2   char0
=avancarelem     lista2   1      OK
=obtervalorelem  lista2   char1
=avancarelem     lista2   1      OK
=obtervalorelem  lista2   char2
=avancarelem     lista2   1      OK
=obtervalorelem  lista2   char3
=avancarelem     lista2   1      FimLista

== Verificar a lista andando demais de trás para diante
=avancarelem     lista2   -10     FimLista
=obtervalorelem  lista2   char0

== Verificar a lista andando demais de frente para trás
=avancarelem     lista2   10     FimLista
=obtervalorelem  lista2   char3

== Destruir a lista
=destruirlista   lista2

// fim
